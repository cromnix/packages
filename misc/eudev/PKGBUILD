# Maintainer: artoo <artoo@manjaro.org>
# Contributor: Alexey D. <lq07829icatm@rambler.ru>
# Contributor: Ivailo Monev <xakepa10@gmail.com>

_udev_ver=220

pkgbase=eudev
pkgname=('eudev' 'libeudev')
pkgver=3.2.2
pkgrel=2
arch=('i686' 'x86_64' 'armv7h')
url="http://www.gentoo.org/proj/en/eudev/"
license=('GPL')
makedepends=('gobject-introspection' 'gperf' 'gtk-doc' 'intltool' 'kmod')
options=('!libtool')
source=("$pkgname-$pkgver.tar.gz::https://github.com/gentoo/eudev/archive/v${pkgver}.tar.gz"
        'initcpio_hooks'
        'initcpio_install'
        'udev-hwdb.hook'
        'udev-hwdb-path.patch')
sha256sums=('80628191448886846ab9cc0e2af6782358a0afb81d5b4f8cd9d982d2626cca31'
            '892ce43218e0a458981bbce451252c8987dc398e60b8de288e7542b8f2409c13'
            '77dd1fd318b4456409aceb077f060b87944defb07cf39d29ad1968dc6f361875'
            '846e9ddbb95c8394ba7efe75107cc1308426921bc042f5d6b48fa4c2dcbac151'
            'e05e468d15d09fb49343f33bc8d21c08f54db3ecff412dc35d4acb0630f869a3')

prepare(){
    cd "${srcdir}/${pkgbase}-${pkgver}"
    patch -Np 1 -i ${srcdir}/udev-hwdb-path.patch
    sed -e 's/GROUP="dialout"/GROUP="uucp"/' \
        -e 's/GROUP="cdrom"/GROUP="optical"/' \
        -e 's/GROUP="tape"/GROUP="storage"/' \
        -i rules/*.rules
    ./autogen.sh
}

build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"

    ./configure \
        --prefix=/usr \
        --with-rootprefix=/usr \
        --sysconfdir=/etc \
        --libdir=/usr/lib \
        --sbindir=/usr/bin \
        --enable-introspection \
        --enable-kmod \
        --enable-manpages \
        --enable-split-usr

    make
}

package_eudev() {
    pkgdesc="The userspace dev tools (udev) forked by Gentoo"
    groups=('base-nosystemd')
    provides=("udev=${_udev_ver}")
    depends=("libeudev" 'kbd' 'kmod' 'hwids')
    optdepends=('eudev-systemd: makes packages compiled with systemd features run'
                'udev-openrc: udev starup scripts for openrc')
    backup=('etc/udev/udev.conf')


    cd "${srcdir}/${pkgbase}-${pkgver}"

    make DESTDIR="${pkgdir}" install

    rm -rv "$pkgdir"/usr/lib/pkgconfig
    rm -v "$pkgdir"/usr/include/libudev.h
    rm -v "$pkgdir"/usr/lib/libudev*.{so*,a}

    install -Dm644 "${srcdir}/initcpio_hooks" "${pkgdir}/usr/lib/initcpio/hooks/udev"
    install -Dm644 "${srcdir}/initcpio_install" "${pkgdir}/usr/lib/initcpio/install/udev"
    install -Dm644 "$srcdir/udev-hwdb.hook" "$pkgdir/usr/share/libalpm/hooks/udev-hwdb.hook"
}

package_libeudev() {
    pkgdesc="eudev client libraries"
    groups=('base-devel-nosystemd')
    depends=('glibc')
    optdepends=('libeudev-systemd: systemd client libraries')
    conflicts=('libudev.so')
    provides=("libudev=${_udev_ver}" 'libudev.so')

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make -C src/libudev DESTDIR="$pkgdir" install
}
